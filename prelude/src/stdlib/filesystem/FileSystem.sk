/**
 * Copyright (c) Facebook, Inc. and its affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

// Module for manipulating the Files and Directories.

// TODO: What module should this go in?
@cpp_extern("SKIP_getcwd")
@may_alloc
native fun getcwd(): String;

module FileSystem;

class OpenOptions{
  read: Bool = false,
  write: Bool = false,
  append: Bool = false,
  truncate: Bool = false,
  create: Bool = false,
  create_new: Bool = false,
  mode: Int = 0o777,
} {
  fun flags(): Int {
    // TODO: Check validity of flags.
    Posix.open_flags(
      if (this.read) 1 else 0,
      if (this.write) 1 else 0,
      if (this.append) 1 else 0,
      if (this.truncate) 1 else 0,
      if (this.create) 1 else 0,
      if (this.create_new) 1 else 0,
    )
  }
}

mutable class File(
  fileno: Int,
  path: String = "unknown",
) uses IO.Read, IO.Write, IO.IsTerminal {
  static fun open(
    path: String,
    opts: OpenOptions = OpenOptions{},
  ): mutable File {
    mutable File(Posix.open(path, opts.flags(), opts.mode), path)
  }

  readonly fun isatty(): Bool {
    Posix.isatty(this.fileno)
  }

  mutable fun read(buf: mutable MutableSlice<UInt8>): Result<Int, IO.Error> {
    rv = Posix.read(this.fileno, buf.mptr(), buf.size());
    if (rv >= 0) {
      Success(rv)
    } else {
      Failure(IO.Error(-rv))
    }
  }

  mutable fun write(buf: readonly Slice<UInt8>): Result<Int, IO.Error> {
    rv = Posix.write(this.fileno, buf.ptr(), buf.size());
    if (rv >= 0) {
      Success(rv)
    } else {
      Failure(IO.Error(-rv))
    }
  }

  mutable fun close(): void {
    Posix.close(this.fileno)
  }
}

@cpp_extern("SKIP_open_file")
native fun open_file(fileName: String): String;

// This is really broken - you can't read a file into a String without an
// encoding!
fun readTextFile(filename: String): String {
  open_file(filename);
}

fun writeTextFile(filename: String, contents: String): void {
  string_to_file(contents, filename);
}

@synonym("appendFile")
@synonym("appendFileSync")
@cpp_runtime
native fun appendTextFile(filename: String, contents: String): void;

@cpp_extern("SKIP_check_if_file_exists")
native fun exists(filename: String): Bool;

fun isDirectory(filename: String): Bool {
  is_directory(filename);
}

fun ensureDirectory(dirname: String): void {
  ensure_directory(dirname);
}

// Returns the contents of dirname without the 'dirname' prefix.
fun readDirectory(dirname: String): Array<String> {
  dh = opendir(dirname);
  res = mutable Vector[];
  loop {
    fn = readdir(dh);
    if (fn == "") {
      break void
    } else if (fn == "." || fn == "..") {
      continue
    };
    res.push(fn)
  };
  closedir(dh);
  res.toArray().sorted()
}

// Searches in dirname, for a file named filename.
// If not found, continues searching in dirname's parent directory.
// Returns the directory of the found file, or None() if not found.
fun findNearestFile(dirname: String, filename: String): ?String {
  if (exists(Path.join(dirname, filename))) {
    Some(dirname);
  } else if (Path.isRoot(dirname)) {
    None();
  } else {
    findNearestFile(Path.dirname(dirname), filename);
  };
}

// Returns all the files in dirname, recursively.
// Note that it does NOT return the directories contained in dirname.
//
// fileFilter is applied to files only, not directories.
fun readFilesRecursive(
  dirname: String,
  fileFilter: String -> Bool = _ ~> true,
  directoryFilter: String -> Bool = _ ~> true,
): Vector<String> {
  result: mutable Vector<String> = mutable Vector[];

  // TODO: Recursive lambdas.
  processDirectory: String -> void = _ -> void;
  !processDirectory = dir -> {
    // Ensure predictable order.
    for (entry in readDirectory(dir)) {
      entryPath = Path.join(dir, entry);
      if (isDirectory(entryPath)) {
        if (directoryFilter(entryPath)) {
          processDirectory(entryPath);
        };
      } else if (fileFilter(entryPath)) {
        result.push(entryPath);
      };
    };
  };
  processDirectory(Path.normalize(dirname));

  result.chill();
}

// TODO: Rename the C++ entrypoints and merge these into the
// public API above.
@cpp_runtime
private native fun .open_file(String): String;

@debug
@cpp_extern("SKIP_string_to_file")
private native fun .string_to_file(s: String, file: String): void;

@cpp_extern("SKIP_is_directory")
private native fun is_directory(filename: String): Bool;

@debug
@cpp_runtime
private native fun ensure_directory(dirname: String): void;

@cpp_extern("SKIP_opendir")
private native fun opendir(dirname: String): Int;

@cpp_extern("SKIP_readdir")
private native fun readdir(dirhandle: Int): String;

@cpp_extern("SKIP_closedir")
private native fun closedir(dirhandle: Int): void;

@cpp_extern("SKIP_realpath")
native fun realpath(path: String): String;

module end;
