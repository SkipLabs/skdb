module Sknpm;

fun check(): (
  Cli.Command,
  (Skargo.GlobalContext, Cli.ParseResults) ~> Result<void, void>,
) {
  (
    Cli.Command("check")
      .short("c")
      .about("Run tsc on ts source files")
      .arg(
        Cli.Arg::string("manifest-path")
          .value_name("PATH")
          .about("Path to Skargo.toml"),
      )
      .arg(
        Cli.Arg::string("target-dir")
          .value_name("PATH")
          .about("Directory for all generated artifacts"),
      )
      .args(Skargo.kProfileArgs)
      .arg(
        Cli.Arg::string("bin")
          .repeatable()
          .value_name("NAME")
          .about("Build only the specified binary"),
      )
      .arg(
        Cli.Arg::bool("test")
          .short("t")
          .long("test")
          .about("Run tsc on ts tests files, with built package"),
      ),
    (gctx, args) ~> execCommand(gctx, args, execCheck),
  )
}

fun execCheck(
  brunner: BuildRunner,
  args: Cli.ParseResults,
): Result<void, void> {
  ws = brunner.bctx.ws;
  gctx = brunner.bctx.gctx;
  test = args.getBool("test");
  root = ws.package.manifest.package_id.source_id.url;
  tsDir = if (test) {
    Path.join(root, "ts/tests")
  } else {
    Path.join(root, "ts/src")
  };
  if (!FileSystem.exists(tsDir)) {
    gctx.console.error("`" + tsDir + "` not founds.");
    return Failure(void);
  };
  install(tsDir, gctx.console)?;
  if (test) {
    name = ws.package.manifest.package_id.name;
    outdir = Path.join(Path.join(tsDir, "node_modules"), name);
    doBuild(brunner, args, false, true, Some(outdir)) match {
    | Success _ -> void
    | _ -> return Failure(void)
    }
  };
  tsc(tsDir, gctx.console)
}

module end;
